include(CMakeFindDependencyMacro)

# Boost is a required dependency
find_dependency(Boost REQUIRED)

# Was TTK built with optional dependencies?

if (@TTK_ENABLE_MPI@)
  find_package(MPI REQUIRED)
endif()

if (@TTK_ENABLE_OPENMP@)
  find_dependency(OpenMP REQUIRED)
endif()

if (@TTK_ENABLE_QHULL@ AND @Qhull_FOUND@)
  find_dependency(Qhull REQUIRED)
endif()

if (@TTK_ENABLE_TORCH@ AND TORCH_FOUND)
  find_dependency(Torch REQUIRED)
endif()

if (@TTK_ENABLE_ZFP@ AND NOT @TTK_ENABLE_SHARED_BASE_LIBRARIES@)
  set(ZFP_DIR "@ZFP_DIR@" CACHE PATH "Use TTK ZFP dir" FORCE)
  find_dependency(ZFP REQUIRED @ZFP_DIR@)
endif()

if (@TTK_ENABLE_ZLIB@ AND NOT @TTK_ENABLE_SHARED_BASE_LIBRARIES@)
  find_dependency(ZLIB REQUIRED)
endif()

if (@TTK_ENABLE_SCIKIT_LEARN@ AND NOT @TTK_ENABLE_SHARED_BASE_LIBRARIES@)
  find_package(Python3 COMPONENTS Development NumPy)
endif()

# Include the actual targets for TTK Base
include("${CMAKE_CURRENT_LIST_DIR}/TTKBaseTargets.cmake")
